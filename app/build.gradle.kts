plugins {
    id("org.fcitx.fcitx5.android.app-convention")
    id("org.fcitx.fcitx5.android.native-app-convention")
    id("org.fcitx.fcitx5.android.build-metadata")
    id("org.fcitx.fcitx5.android.data-descriptor")
    id("org.fcitx.fcitx5.android.fcitx-component")
    alias(libs.plugins.kotlin.parcelize)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.ksp)
}

import java.util.Properties

android {
    namespace = "org.fcitx.fcitx5.android"

    // Keep our bundled keypress sounds uncompressed so SoundPool can mmap/load them reliably on
    // every device (a compressed res/raw asset can return sample id 0 on some ROMs).
    aaptOptions {
        noCompress.add("wav")
        // Font.Builder(AssetManager) needs an uncompressed OTF so it can mmap the bundled CJK face.
        noCompress.add("otf")
    }

    defaultConfig {
        applicationId = "tapfeet.ime"
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"

        @Suppress("UnstableApiUsage")
        externalNativeBuild {
            cmake {
                targets(
                    // jni
                    "native-lib",
                    // copy fcitx5 built-in addon libraries
                    "copy-fcitx5-modules",
                    // android specific modules
                    "androidfrontend",
                    "androidkeyboard",
                    "androidnotification",
                    "rime"
                )
            }
        }
    }

    buildFeatures {
        viewBinding = true
        resValues = true
    }

     // 签名配置从 local.properties 读取（该文件已被 .gitignore 忽略，不会进入版本库），
    // 避免密钥密码泄露到 git 历史。CI / 未配置 keystore 的环境不会创建 release signingConfig，
    // 此时不应执行 assembleRelease（缺少签名配置会失败，属预期行为）；发布签名仅应在具备
    // keystore 的本地环境或带 secrets 的 release 流水线中进行。
    val keystoreProps = Properties()
    rootProject.file("local.properties").takeIf { it.exists() }?.inputStream()?.use {
        keystoreProps.load(it)
    }
    val hasReleaseKeystore = keystoreProps.getProperty("RELEASE_STORE_FILE") != null

    if (hasReleaseKeystore) {
        // 2. 配置签名
        signingConfigs {
            create("release") {
                storeFile = file(keystoreProps.getProperty("RELEASE_STORE_FILE")!!)
                storePassword = keystoreProps.getProperty("RELEASE_STORE_PASSWORD")
                keyAlias = keystoreProps.getProperty("RELEASE_KEY_ALIAS")
                keyPassword = keystoreProps.getProperty("RELEASE_KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        release {
            if (hasReleaseKeystore) {
                signingConfig = signingConfigs.getByName("release")
            }
            resValue("mipmap", "app_icon", "@mipmap/ic_launcher")
            resValue("mipmap", "app_icon_round", "@mipmap/ic_launcher_round")
            resValue("string", "app_name", "@string/app_name_release")
            proguardFile("proguard-rules.pro")
        }
        debug {
            resValue("mipmap", "app_icon", "@mipmap/ic_launcher")
            resValue("mipmap", "app_icon_round", "@mipmap/ic_launcher_round")
            resValue("string", "app_name", "@string/app_name_debug")
        }
    }
    androidResources {
        @Suppress("UnstableApiUsage")
        generateLocaleConfig = true
    }
}



// 把 docs/更新内容.txt 同步为 res/raw/changelog.txt（单一编辑源 = docs/），
// 在 preBuild 前执行，避免两份内容漂移。
val syncChangelog by tasks.registering(Copy::class) {
    from(rootProject.file("docs/更新内容.txt"))
    into(layout.projectDirectory.dir("src/main/res/raw"))
    rename { "changelog.txt" }
}
tasks.named("preBuild").configure { dependsOn(syncChangelog) }

fcitxComponent {
    includeLibs = listOf(
        "fcitx5",
        "fcitx5-lua",
        "libime",
        "fcitx5-chinese-addons"
    )
    // exclude (delete immediately after install) tables that nobody would use
    excludeFiles = listOf("cangjie", "erbi", "qxm", "wanfeng").map {
        "usr/share/fcitx5/inputmethod/$it.conf"
    }
    installPrebuiltAssets = true
    modifyFiles = mapOf(
        "usr/share/fcitx5/inputmethod/rime.conf" to { file ->
            val text = file.readText()
            file.writeText(
                text.replace("Name=Rime", "Name=中州韵拼音")
                    .replace("Label=ㄓ", "Label=韵")
            )
        }
    )
}

generateDataDescriptor {
    // librime looks for OpenCC data under rime-data/opencc; the app already ships OpenCC.
    symlinks.put("usr/share/rime-data/opencc", "usr/share/opencc")
}

ksp {
    arg("room.schemaLocation", "$projectDir/schemas")
}

dependencies {
    ksp(project(":codegen"))
    implementation(project(":lib:fcitx5"))
    implementation(project(":lib:fcitx5-lua"))
    implementation(project(":lib:libime"))
    implementation(project(":lib:fcitx5-chinese-addons"))
    implementation(project(":lib:common"))
    implementation(libs.kotlinx.coroutines)
    implementation(libs.kotlinx.serialization.json)
    implementation(libs.androidx.activity)
    implementation(libs.androidx.appcompat)
    implementation(libs.androidx.autofill)
    implementation(libs.androidx.constraintlayout)
    implementation(libs.androidx.coordinatorlayout)
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.lifecycle.viewmodel)
    implementation(libs.androidx.lifecycle.livedata)
    implementation(libs.androidx.lifecycle.runtime)
    implementation(libs.androidx.lifecycle.common)
    implementation(libs.androidx.lifecycle.service)
    implementation(libs.androidx.navigation.fragment)
    implementation(libs.androidx.navigation.ui)
    implementation(libs.androidx.paging)
    implementation(libs.androidx.preference)
    implementation(libs.androidx.recyclerview)
    ksp(libs.androidx.room.compiler)
    implementation(libs.androidx.room.runtime)
    implementation(libs.androidx.room.ktx)
    implementation(libs.androidx.room.paging)
    implementation(libs.androidx.startup)
    implementation(libs.androidx.viewpager2)
    implementation(libs.material)
    implementation(libs.arrow.core)
    implementation(libs.arrow.functions)
    implementation(libs.imagecropper)
    implementation(libs.flexbox)
    implementation(libs.dependency)
    implementation(libs.timber)
    implementation(libs.splitties.bitflags)
    implementation(libs.splitties.dimensions)
    implementation(libs.splitties.resources)
    implementation(libs.splitties.views.dsl)
    implementation(libs.splitties.views.dsl.appcompat)
    implementation(libs.splitties.views.dsl.constraintlayout)
    implementation(libs.splitties.views.dsl.coordinatorlayout)
    implementation(libs.splitties.views.dsl.recyclerview)
    implementation(libs.splitties.views.recyclerview)
    implementation(libs.aboutlibraries.core)
    implementation(libs.okhttp)
    // 本地语音输入（SenseVoice via sherpa-onnx）：模型压缩包为 tar.bz2，需要解包
    implementation(files("libs/sherpa-onnx.aar"))
    implementation(libs.commons.compress)
    implementation(libs.commons.io)
    testImplementation(libs.junit)
    androidTestImplementation(libs.androidx.test.runner)
    androidTestImplementation(libs.androidx.test.rules)
    androidTestImplementation(libs.androidx.lifecycle.testing)
    androidTestImplementation(libs.junit)
}

configurations {
    all {
        // remove Baseline Profile Installer or whatever it is...
        exclude(group = "androidx.profileinstaller", module = "profileinstaller")
        // remove unwanted splitties libraries...
        exclude(group = "com.louiscad.splitties", module = "splitties-appctx")
        exclude(group = "com.louiscad.splitties", module = "splitties-systemservices")
    }
}
